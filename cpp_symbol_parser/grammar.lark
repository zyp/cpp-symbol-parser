?start: "_Z" encoding

encoding: function

function: name bare_function_type

bare_function_type: type+

name: nested_name
    | unscoped_name
    | unscoped_template_name template_args -> template

nested_name: "N" prefix unqualified_name "E" -> prefix
           | "N" template_prefix template_args "E" -> template

prefix: unqualified_name
      | prefix unqualified_name
      | template_prefix template_args -> template
      | substitution

unscoped_name: unqualified_name
             | "St" unqualified_name -> std_scoped_name

unscoped_template_name: unscoped_name
                      | substitution

unqualified_name: source_name

template_prefix: unqualified_name
               | prefix unqualified_name -> prefix
               | substitution

template_args: "I" template_arg+ "E"

template_arg: type
            | "X" expression "E"
            | expr_primary
            | "J" template_arg* "E" -> template_parameter_pack

substitution: "S" [SEQ_ID] "_"
            | SUB_STD_X

SEQ_ID: /[0-9A-Z]+/
SUB_STD: "St"
SUB_STD_X: /S[tabsiod]/



expression: "tl" type braced_expression* "E" -> aggregate_init
          | expr_primary

braced_expression: expression

expr_primary: "L" type NUMBER "E" -> literal


type: builtin_type
    | class_enum_type
    | array_type
    | substitution

builtin_type: BUILTIN_TYPE
BUILTIN_TYPE: /[vwbcahstijlmxynofdegz]/

class_enum_type: name

array_type: "A" NUMBER "_" type

source_name:     source_name_inner
           | "1" source_name_inner IDENTIFIER ~ 10
           | "2" source_name_inner IDENTIFIER ~ 20
           | "3" source_name_inner IDENTIFIER ~ 30
           | "4" source_name_inner IDENTIFIER ~ 40
           | "5" source_name_inner IDENTIFIER ~ 50
           | "6" source_name_inner IDENTIFIER ~ 60
           | "7" source_name_inner IDENTIFIER ~ 70
           | "8" source_name_inner IDENTIFIER ~ 80
           | "9" source_name_inner IDENTIFIER ~ 90

source_name_inner: "0"
                     | "1" FIRST_IDENTIFIER
                     | "2" FIRST_IDENTIFIER IDENTIFIER ~ 1
                     | "3" FIRST_IDENTIFIER IDENTIFIER ~ 2
                     | "4" FIRST_IDENTIFIER IDENTIFIER ~ 3
                     | "5" FIRST_IDENTIFIER IDENTIFIER ~ 4
                     | "6" FIRST_IDENTIFIER IDENTIFIER ~ 5
                     | "7" FIRST_IDENTIFIER IDENTIFIER ~ 6
                     | "8" FIRST_IDENTIFIER IDENTIFIER ~ 7
                     | "9" FIRST_IDENTIFIER IDENTIFIER ~ 8

FIRST_IDENTIFIER: /[_A-Za-z]/
IDENTIFIER: /[_A-Za-z0-9]/

NUMBER: /\d+/
